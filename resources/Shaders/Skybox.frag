#version 450 core

layout(location = 0) in vec3 v_WorldPos;
layout (location = 0) out vec4 out_color;

layout (binding = 1) uniform samplerCube samplerCubeMap;
layout (std140, binding = 512) uniform DynamicSky
{
	vec4 rayOrigin;
	vec4 sunPosition;
	vec4 rayleighScatteringCoeff;

	float sunIntensity;
	float planetRadius;
	float atmosphereRadius;
	float mieScatteringCoeff;

	float rayleighScale;
	float mieScale;
	float mieScatteringDirection;
    uint  numCirrusCloudsIterations;

    uint  numCumulusCloudsIterations;
};

layout(push_constant) uniform PushConsts 
{
	uint state;
};

#define PI 3.141592
#define iSteps 16
#define jSteps 8

vec2 rsi(vec3 r0, vec3 rd, float sr) {
    // ray-sphere intersection that assumes
    // the sphere is centered at the origin.
    // No intersection when result.x > result.y
    float a = dot(rd, rd);
    float b = 2.0 * dot(rd, r0);
    float c = dot(r0, r0) - (sr * sr);
    float d = (b*b) - 4.0*a*c;
    if (d < 0.0) return vec2(1e5,-1e5);
    return vec2(
        (-b - sqrt(d))/(2.0*a),
        (-b + sqrt(d))/(2.0*a)
    );
}

// from: https://github.com/wwwtyro/glsl-atmosphere
vec3 atmosphere(vec3 r, vec3 r0, vec3 pSun, float iSun, float rPlanet, float rAtmos, vec3 kRlh, float kMie, float shRlh, float shMie, float g) {
    // Normalize the sun and view directions.
    pSun = normalize(pSun);
    r = normalize(r);

    // Calculate the step size of the primary ray.
    vec2 p = rsi(r0, r, rAtmos);
    if (p.x > p.y) return vec3(0,0,0);
    p.y = min(p.y, rsi(r0, r, rPlanet).x);
    float iStepSize = (p.y - p.x) / float(iSteps);

    // Initialize the primary ray time.
    float iTime = 0.0;

    // Initialize accumulators for Rayleigh and Mie scattering.
    vec3 totalRlh = vec3(0,0,0);
    vec3 totalMie = vec3(0,0,0);

    // Initialize optical depth accumulators for the primary ray.
    float iOdRlh = 0.0;
    float iOdMie = 0.0;

    // Calculate the Rayleigh and Mie phases.
    float mu = dot(r, pSun);
    float mumu = mu * mu;
    float gg = g * g;
    float pRlh = 3.0 / (16.0 * PI) * (1.0 + mumu);
    float pMie = 3.0 / (8.0 * PI) * ((1.0 - gg) * (mumu + 1.0)) / (pow(1.0 + gg - 2.0 * mu * g, 1.5) * (2.0 + gg));

    // Sample the primary ray.
    for (int i = 0; i < iSteps; i++) {

        // Calculate the primary ray sample position.
        vec3 iPos = r0 + r * (iTime + iStepSize * 0.5);

        // Calculate the height of the sample.
        float iHeight = length(iPos) - rPlanet;

        // Calculate the optical depth of the Rayleigh and Mie scattering for this step.
        float odStepRlh = exp(-iHeight / shRlh) * iStepSize;
        float odStepMie = exp(-iHeight / shMie) * iStepSize;

        // Accumulate optical depth.
        iOdRlh += odStepRlh;
        iOdMie += odStepMie;

        // Calculate the step size of the secondary ray.
        float jStepSize = rsi(iPos, pSun, rAtmos).y / float(jSteps);

        // Initialize the secondary ray time.
        float jTime = 0.0;

        // Initialize optical depth accumulators for the secondary ray.
        float jOdRlh = 0.0;
        float jOdMie = 0.0;

        // Sample the secondary ray.
        for (int j = 0; j < jSteps; j++) {

            // Calculate the secondary ray sample position.
            vec3 jPos = iPos + pSun * (jTime + jStepSize * 0.5);

            // Calculate the height of the sample.
            float jHeight = length(jPos) - rPlanet;

            // Accumulate the optical depth.
            jOdRlh += exp(-jHeight / shRlh) * jStepSize;
            jOdMie += exp(-jHeight / shMie) * jStepSize;

            // Increment the secondary ray time.
            jTime += jStepSize;
        }

        // Calculate attenuation.
        vec3 attn = exp(-(kMie * (iOdMie + jOdMie) + kRlh * (iOdRlh + jOdRlh)));

        // Accumulate scattering.
        totalRlh += odStepRlh * attn;
        totalMie += odStepMie * attn;

        // Increment the primary ray time.
        iTime += iStepSize;

    }

    // Calculate and return the final color.
    return iSun * (pRlh * kRlh * totalRlh + pMie * kMie * totalMie);
}

float hash(float n)
{
  return fract(sin(n) * 43758.5453123);
}

float noise(vec3 x)
{
  vec3 f = fract(x);
  float n = dot(floor(x), vec3(1.0, 157.0, 113.0));
  return mix(mix(mix(hash(n +   0.0), hash(n +   1.0), f.x),
                 mix(hash(n + 157.0), hash(n + 158.0), f.x), f.y),
             mix(mix(hash(n + 113.0), hash(n + 114.0), f.x),
                 mix(hash(n + 270.0), hash(n + 271.0), f.x), f.y), f.z);
}

const mat3 m = mat3(0.0, 1.60,  1.20, -1.6, 0.72, -0.96, -1.2, -0.96, 1.28);
float fbm(vec3 p)
{
    float f = 0.0;
    f += noise(p) / 2; p = m * p * 1.1;
    f += noise(p) / 4; p = m * p * 1.2;
    f += noise(p) / 6; p = m * p * 1.3;
    f += noise(p) / 12; p = m * p * 1.4;
    f += noise(p) / 24;
    return f;
}

const float cirrus = 0.4;
const float cumulus = 0.8;
const float time = 0.0;
const float Br = 0.0025;
const float Bm = 0.0003;
const float g =  0.9800;
const vec3 nitrogen = vec3(0.650, 0.570, 0.475);
const vec3 Kr = Br / pow(nitrogen, vec3(4.0));
const vec3 Km = Bm / pow(nitrogen, vec3(0.84));

vec3 DrawClouds(vec3 color)
{
    vec3 pos = -v_WorldPos;
    if (pos.y < 0)
    {
        return color;
    }

    vec3 result = vec3(0);
    float sunPos = 0.1;
    vec3 day_extinction = exp(-exp(-((pos.y + sunPos * 4.0) * (exp(-pos.y * 16.0) + 0.1) / 80.0) / Br) * (exp(-pos.y * 16.0) + 0.1) * Kr / Br) * exp(-pos.y * exp(-pos.y * 8.0 ) * 4.0) * exp(-pos.y * 2.0) * 4.0;
    vec3 night_extinction = vec3(1.0 - exp(sunPos)) * 0.2;
    vec3 extinction = mix(day_extinction, night_extinction, -sunPos * 0.2 + 0.5);

    result = color;
    // Cirrus Clouds
    for (int i = 0; i < numCirrusCloudsIterations; i++)
    {
        float density = smoothstep(1.0 - cirrus, 1.0, fbm(pos.xyz / pos.y * 2.0 + time * 0.05)) * 0.3;
        result.rgb = mix(result.rgb, extinction * 4.0, density * max(pos.y, 0.0));
    }

    // Cumulus Clouds
    for (int i = 0; i < numCumulusCloudsIterations; i++)
    {
      float density = smoothstep(1.0 - cumulus, 1.0, fbm((0.7 + float(i) * 0.01) * pos.xyz / pos.y + time * 0.3));
      result.rgb = mix(result.rgb, vec3(0.25) * density * 5.0, min(density, 1.0) * max(pos.y, 0.0));
    }

    return result;
}

void main()
{
	vec4 finalColor = vec4(0);
	switch(state)
	{
		case 0:
		{
			finalColor = texture(samplerCubeMap, v_WorldPos);
			break;
		}
		case 1:
		{
			finalColor.rgb = atmosphere(
            normalize(-v_WorldPos),         // normalized ray direction
            rayOrigin.xyz,                  // ray origin
            sunPosition.xyz,                // position of the sun
            sunIntensity,                   // intensity of the sun
            planetRadius,                   // radius of the planet in meters
            atmosphereRadius,               // radius of the atmosphere in meters
            rayleighScatteringCoeff.xyz,    // Rayleigh scattering coefficient
            mieScatteringCoeff,             // Mie scattering coefficient
            rayleighScale,                  // Rayleigh scale height
            mieScale,                       // Mie scale height
            mieScatteringDirection          // Mie preferred scattering direction
			);   

            finalColor.rgb = DrawClouds(finalColor.rgb);
		}
	}

	out_color = vec4(finalColor.rgb, 0.0);
}